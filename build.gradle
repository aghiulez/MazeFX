plugins {
	id 'org.springframework.boot' version '2.3.1.RELEASE'
	id 'io.spring.dependency-management' version '1.0.9.RELEASE'
	id 'java'
	id 'application'
	id 'com.google.osdetector' version "1.6.2"
}

group = 'com.aramatthew'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

repositories {
	mavenCentral()
}

ext.platform = osdetector.os == 'osx' ? 'mac' : osdetector.os == 'windows' ? 'win' : osdetector.os

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter'
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}

	compile "org.openjfx:javafx-base:11:$platform"
	compile "org.openjfx:javafx-graphics:11:$platform"
	compile "org.openjfx:javafx-controls:11:$platform"
	compile "org.openjfx:javafx-fxml:11:$platform"
	compile "org.openjfx:javafx-media:11:$platform"
	compile "org.openjfx:javafx-web:11:$platform"
}


compileJava {
	doFirst {
		options.compilerArgs = [
				'--module-path', classpath.asPath,
				'--add-modules', 'javafx.fxml,javafx.controls'
		]
	}
}

run {
	doFirst {
		jvmArgs = [
				'--module-path', classpath.asPath,
				'--add-modules', 'javafx.fxml,javafx.controls'
		]
	}
}



test {
	useJUnitPlatform()
}
